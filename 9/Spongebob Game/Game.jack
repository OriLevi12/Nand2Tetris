/** Manages the game logic and main game loop */
class Game {
    field SpongeBob player;      // The player character
    field Jellyfish jellyfish;   // The current falling jellyfish
    field MotherJellyfish motherJellyfish;  // The mother jellyfish
    field int score;             // Player's score
    field int counter;           // Counter for mother jellyfish spawn
    field boolean exit;          // Game exit flag
    
    /** Constructs a new game */
    constructor Game new() {
        let player = SpongeBob.new(256, 210, 16, 10);  // x, y, size, speed
        do Random.waitForRandomSeed();  // Get random seed from player input
        let jellyfish = Jellyfish.new(Random.randRange(400), 0, 16, 4);  // Random x position
        let motherJellyfish = MotherJellyfish.new(Random.randRange(400), 0, 32, 4);  // Random x position, size 32
        do motherJellyfish.setActive(false);
        let score = 0;
        let counter = 0;
        let exit = false;
        return this;
    }

    /** Runs the main game loop */
    method void run() {
        var char key;
        do drawScore();
        
        while (~exit) {
            // Get current key
            let key = Keyboard.keyPressed();
            // Handle exit
            if (key = 81) { 
                let exit = true; 
            }       // q key
            
            // Handle player movement
            if (key = 130) {  // left arrow
                do player.setDirection(1);
            }
            else {
                if (key = 132) {  // right arrow
                    do player.setDirection(2);
                }
                else {
                    do player.setDirection(0);  // no movement
                }
            }
            
            do player.move();
            do jellyfish.move();

            // Check for catch
            if (jellyfish.intersects(player)) {
                let score = score + 1;
                let counter = counter + 1;
                do drawScore();
                do jellyfish.erase();
                do jellyfish.dispose();
                let jellyfish = Jellyfish.new(Random.randRange(400), 0, 16, jellyfish.getSpeed());  // Use current jellyfish speed
            }

            // Check if jellyfish is in middle and counter is 5
            if ((counter = 5) & jellyfish.isJellyfishInMiddle()) {
                /** When player catches 5 jellyfish:
                 * - Mother jellyfish is activated
                 * - Jellyfish speed increases by 1
                 * - Mother jellyfish speed increases by 1
                 * - Player speed increases by 2
                 * - Counter resets to 0 */
                do motherJellyfish.setActive(true);
                do jellyfish.setSpeed(jellyfish.getSpeed()+ 1);
                do motherJellyfish.setSpeed(motherJellyfish.getSpeed()+1);
                do player.setSpeed(player.getSpeed()+2);
                let counter = 0;
            }
            
            // Update mother jellyfish if active
            if (motherJellyfish.isActive()) {
                do motherJellyfish.move();
                if (motherJellyfish.intersects(player)) {
                    let score = score + 3;
                    do drawScore();
                    do motherJellyfish.erase();
                    do motherJellyfish.dispose();
                    let motherJellyfish = MotherJellyfish.new(Random.randRange(400), 0, 32, motherJellyfish.getSpeed());  // Use current mother jellyfish speed
                    do motherJellyfish.setActive(false);
                }
                else {
                    if (motherJellyfish.hasReachedBottom()) {
                        let exit = true;
                    }
                }
            }
            
            // Check if jellyfish reached bottom
            if (jellyfish.hasReachedBottom()) {
                let exit = true;
            }
            
            do Sys.wait(50);  // Reduced wait time for more responsive movement
        }

        // Show game over screen
        do Screen.clearScreen();
        do Output.moveCursor(10, 27);
        do Output.printString("Game Over!");
        do Output.moveCursor(12, 27);
        do Output.printString("Final Score: ");
        do Output.printInt(score);
        return;
    }

    /** Disposes of this game */
    method void dispose() {
        do player.dispose();
        do jellyfish.dispose();
        do motherJellyfish.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Draws the current score */
    method void drawScore() {
        do Output.moveCursor(0, 0);
        do Output.printString("Score: ");
        do Output.printInt(score);
        return;
    }
} 