// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    // Handle Increment First
    Inc16(in=outIN, out=incOut);
    Mux16(a=outIN, b=incOut, sel=inc, out=incMuxOut);

    // Handle Load (if load=1, override increment)
    Mux16(a=incMuxOut, b=in, sel=load, out=loadMuxOut);

    //Handle Reset (if reset=1, force 0)
    Mux16(a=loadMuxOut, b=false, sel=reset, out=nextVal);

    // Store the final value in the Register
    Register(in=nextVal, load=true, out=outIN);

    Or16(a=outIN, b=outIN, out=out);

}
